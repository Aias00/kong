// https://stackoverflow.com/questions/43214730/how-to-set-github-commit-status-with-jenkinsfile-not-using-a-pull-request-builde
void setBuildStatus(String message, String state) {
  step([
      $class: "GitHubCommitStatusSetter",
      reposSource: [$class: "ManuallyEnteredRepositorySource", url: "https://github.com/kong/kong-ee"],
      contextSource: [$class: "ManuallyEnteredCommitContextSource", context: "Kong Gateway Smoke Tests"],
      errorHandlers: [[$class: "ChangingBuildStatusErrorHandler", result: "UNSTABLE"]],
      statusResultSource: [ $class: "ConditionalStatusResultSource", results: [[$class: "AnyBuildResult", message: message, state: state]] ]
  ]);
}

// Generate the CLI parameters for any known incompatibilities
def known_incompatibilities(String csv) {
  def known_incompatible = ""
  if (! csv.isEmpty()) {
    known_incompatible = '--known-incompatible ' + csv.split(',').join(' --known-incompatible ')
  }
  return known_incompatible
}

pipeline {
  agent {
    node {
      label 'hybrid'
    }
  }
  options {
    timeout(time: 45, unit: 'MINUTES')
    disableConcurrentBuilds()
  }
  parameters {
    string(
      name: 'DATA_PLANE_KONG_GATEWAY_IMAGE',
      defaultValue: 'kong/kong-gateway:2.4',
      trim: true,
      description: '''<p>Kong Gateway image to use for the data plane</p>
                      <ul>
                        <li><a href="https://hub.docker.com/r/kong/kong-gateway/tags">kong/kong-gateway</a> - Publicly available Kong Gateway images</li>
                        <li><a href="https://hub.docker.com/repository/docker/kong/kong-gateway-internal/tags">kong/kong-gateway-internal</a> - Internal previews and nightly Kong Gateway images</li>
                        <li><a href="https://hub.docker.com/repository/docker/kong/kong-gateway-private/tags">kong/kong-gateway-private</a> - Private Kong Gateway images (Bintray era releases)</li>
                      </ul>
                      <strong>Note</strong>: <b>kong/kong-gateway-internal</b> and <b>kong/kong-gateway-private</b> require 1Password credentials to login (see <i>kongcloudowner</i> for credentials)''')
    string(
      name: 'KNOWN_PLUGIN_INCOMPATIBILITIES',
      defaultValue: 'mocking,opa',
      trim: true,
      description: 'Comma separated list of known incompatible plugins')
    string(
      name: 'CASSANDRA',
      defaultValue: '2.2',
      trim: true,
      description: 'Apache Cassandra version to use')
  }
  environment {
    GITHUB_TOKEN = credentials('github_bot_access_token')
    KONG_LICENSE_DATA = credentials('KONG_LICENSE_DATA')

    // Docker Hub credentials to increase rate-limiting
    DOCKERHUB_KONGCLOUD_PULL = credentials('DOCKERHUB_KONGCLOUD_PULL')

    // Override gojira version (ensure master)
    GOJIRA_VERSION = 'master'

    // Override PATH to include any binary installations
    PATH = "${env.PATH}:~/.local/bin"

    /**
     * Parameter values can be overridden during manual builds. Ensure
     * defaultValue is updated as needed when new next branches are created.
     */
    DATA_PLANE_KONG_GATEWAY_IMAGE = "${params.DATA_PLANE_KONG_GATEWAY_IMAGE}"
    KNOWN_PLUGIN_INCOMPATIBILITIES = "${params.KNOWN_PLUGIN_INCOMPATIBILITIES}"
  }
  stages {
    stage('Login to Docker Hub') {
      steps {
        // Login to Docker Hub (better limits on rate-limiting)
        sh label: 'Login to Docker Hub',
           script: '''echo $DOCKERHUB_KONGCLOUD_PULL_PSW | \
                        docker login \
                          -u $DOCKERHUB_KONGCLOUD_PULL_USR \
                          --password-stdin'''
      }
    }
    stage('Install Gojira Enterprise') {
      steps {
        sh label: 'Clone Gojira enterprise',
           script: 'git clone https://$GITHUB_TOKEN:@github.com/Kong/gojira-enterprise /tmp/gojira'
        sh label: 'Create ~/.local/bin directory',
           script: 'mkdir -p ~/.local/bin'
        sh label: 'Install Gojira enterprise',
           script: 'ln -s $(realpath /tmp/gojira/gojira.sh) $(realpath ~/.local/bin/gojira)'
      }
    }
    stage('Version Compatibility') {
      when {
        anyOf {
          triggeredBy cause: "UserIdCause"
          allOf {
            not {
              anyOf {
                branch 'master'
                branch 'main'
              }
            }
            anyOf {
              changeset "kong/plugins/**"
              changeset "kong/enterprise_edition/redis/**"
              changeset "kong-*.rockspec"
            }
          }
        }
      }
      steps {
        sh label: 'Start Kong Gateway in hybrid mode',
           script: '''gojira hybrid up \
                        -v $(realpath scripts/check-version-compatibility):/check-version-compatibility \
                        --dataplane-image $DATA_PLANE_KONG_GATEWAY_IMAGE'''
        sh label: 'Build Kong Gateway development environment',
           script: '''gojira hybrid run@kong-cp \
                        --dataplane-image $DATA_PLANE_KONG_GATEWAY_IMAGE \
                          make dev'''
        sh label: 'Start Kong Gateway dataplane',
           script: '''gojira hybrid run@kong-dp \
                        --dataplane-image $DATA_PLANE_KONG_GATEWAY_IMAGE \
                        -e KONG_LICENSE_DATA \
                          kong start'''
        sh label: 'Execute version compatibility smoke test',
           script: '''gojira hybrid run@kong-cp \
                        --dataplane-image $DATA_PLANE_KONG_GATEWAY_IMAGE \
                        -e KONG_LICENSE_DATA \
                        -e GITHUB_TOKEN \
                          /check-version-compatibility --skip-enterprise ''' + known_incompatibilities(env.KNOWN_PLUGIN_INCOMPATIBILITIES)
        sh label: 'Clean gojira instances',
           script: '''gojira hybrid nuke \
                        --dataplane-image $DATA_PLANE_KONG_GATEWAY_IMAGE \
                        -f'''
      }
    }
    stage('Apache Cassandra Migrations') {
      steps {
        sh label: 'Start Kong Gateway instance',
           script: 'gojira up --cassandra'
        sh label: 'Build Kong Gateway development environment',
           script: 'gojira run make dev'
        sh label: 'Execute migrations',
           script: 'gojira run kong migrations bootstrap --vv'
        sh label: 'Clean Kong Gateway instance',
           script: 'gojira nuke -f'
      }
    }
  }
  post {
    always {
      sh label: 'Clean Gojira Installation'
         script: 'rm -rf /tmp/gojira ~/.local/bin/gojira'
    }
    success {
      setBuildStatus("Build succeeded", "SUCCESS");
    }
    failure {
      setBuildStatus("Build failed", "FAILURE");
    }
  }
}
