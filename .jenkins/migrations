// Set here migration paths to test. This pipeline lives together with the
// kong code, so keep it up to date and relevant after new releases
// These are just mapping to the arguments to pass to sealy
def paths = [
  [
    name: "Kong EE 1.5.0.4 to here",
    from: "kong-docker-kong-enterprise-edition-docker.bintray.io/kong-enterprise-edition:1.5.0.4-alpine",
    seeds: "paths/0.35_happy_path"
  ],
  [
    name: "Kong EE next/2.1.0.x to here",
    from: "registry.kongcloud.io/kong-enterprise-edition:2.1.0.0-nightly-alpine",
    seeds: "paths/2.x_happy_path"
  ]
]

def builds = [:]
for (x in paths) {
  def path = x // need to assign closure
  builds[path.name] = {
    node('bionic') {
      stage(path.name) {
        stage('prepare') {
          env.BRANCH_OR_TAG = env.BRANCH_NAME ? env.BRANCH_NAME : "refs/tags/${env.TAG_NAME}"

          sh "docker login -u ${env.BINTRAY_USR} -p ${env.BINTRAY_PSW} kong-docker-kong-enterprise-edition-docker.bintray.io"
          sh "docker login -u ${env.DOCKER_REGISTRY_USR} -p ${env.DOCKER_REGISTRY_PSW} registry.kongcloud.io"

          sh "mkdir -p ~/.local/bin"

          sh "git clone https://$GITHUB_TOKEN:@github.com/Kong/kong-gojira.git /tmp/gojira"
          sh 'ln -s $(realpath /tmp/gojira/gojira.sh) $(realpath ~/.local/bin/gojira)'

          sh "git clone https://$GITHUB_TOKEN:@github.com/Kong/sealy.git /tmp/sealy"
          sh 'ln -s $(realpath /tmp/sealy/sealy.sh) $(realpath ~/.local/bin/sealy)'

          sh "git clone -b ${env.BRANCH_OR_TAG} https://$GITHUB_TOKEN:@github.com/Kong/kong-ee.git /tmp/kong"
        }

        stage('test migration (postgres)') {
          env.PATH = env.PATH + ":~/.local/bin"
          sh "sealy --from $path.from --to /tmp/kong --seeds $path.seeds --postgres"
        }

        stage('test migration (cassandra)') {
          env.PATH = env.PATH + ":~/.local/bin"
          sh "sealy --from $path.from --to /tmp/kong --seeds $path.seeds --cassandra"
        }
      }
    }
  }
}

pipeline {
  agent none
  options {
    timeout(time: 100, unit: 'MINUTES')
  }
  environment {
    GITHUB = credentials('GITHUB_TOKEN')
    GITHUB_TOKEN = credentials('GITHUB_TOKEN')
    DOCKER_REGISTRY_URL = "registry.kongcloud.io"
    // This will map DOCKER_REGISTRY_USR and DOCKER_REGISTRY_PSW
    DOCKER_REGISTRY = credentials('DOCKER_REGISTRY')
    // This will map BINTRAY_USR and BINTRAY_PSW
    BINTRAY = credentials('bintray')
    KONG_LICENSE_DATA = credentials('KONG_LICENSE_DATA')
    SEALY_ALWAYS_TEARDOWN = 1
  }

  stages {
    // build parameters are at this point because if not, first build never
    // gets them. If we were to just automate this so it just runs all steps
    // when a tag is pushed, remove this part and set the options to the
    // desired actions

    stage('run migration integration tests') {
      steps {
        script {
          parallel builds
        }
      }
    }
  }
}
