load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")
load("@kong_bindings//:variables.bzl", "KONG_VAR")

filegroup(
    name = "all_srcs",
    srcs = glob(
        include = ["**"],
        exclude = ["*.bazel"],
    ),
)

configure_make(
    name = "gmp",
    configure_command = "configure",
    configure_in_place = True,
    configure_options = [
        "--enable-static=no",
    ] + select({
        "@kong//:arm64-linux-gnu-cross": [
            "--host=aarch64-linux",
        ],
        "@kong//:x86_64-linux-musl-cross": [
            "--host=x86_64-linux-musl",
        ],
        # use less specific CPU to avoid introdcing ADCX instruction
        "@platforms//os:linux": [
            "--build=$(uname -m)-linux-gnu",
        ],
        # otherwise assume on macOS
        "//conditions:default": [],
    }),
    env = select({
        "@platforms//os:macos": {
            # don't use rule_foreign_cc's libtool as archiver as it seems to be a bug
            # see https://github.com/bazelbuild/rules_foreign_cc/issues/947
            "AR": "/usr/bin/ar",
        },
        "//conditions:default": {},
    }),
    lib_source = ":all_srcs",
    # out_lib_dir = "lib",
    out_shared_libs = select({
        "@platforms//os:macos": [
            "libgmp.10.dylib",
        ],
        "//conditions:default": [
            "libgmp.so.10",
        ],
    }),
    targets = [
        "-j" + KONG_VAR["NPROC"],
        "install -j" + KONG_VAR["NPROC"],
    ],
    visibility = ["//visibility:public"],
)
