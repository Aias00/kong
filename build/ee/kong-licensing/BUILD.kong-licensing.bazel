load("@rules_foreign_cc//foreign_cc:defs.bzl", "make")
load("@kong_bindings//:variables.bzl", "KONG_VAR")

filegroup(
    name = "all_srcs",
    srcs = glob(
        include = ["**"],
        exclude = ["*.bazel"],
    ),
)

make(
    name = "kong-licensing",
    args = select({
        "@kong//:fips_flag": [
            "LDFLAGS=\"-L$$EXT_BUILD_DEPS/lib -Wl,-rpath,%s/kong/lib\"" % KONG_VAR["INSTALL_DESTDIR"],
        ],
        "//conditions:default": [
            "LDFLAGS=\"-L$$EXT_BUILD_DEPS/openssl/lib -Wl,-rpath,%s/kong/lib\"" % KONG_VAR["INSTALL_DESTDIR"],
        ],
    }),
    env = select({
        "@kong//:fips_flag": {
            "CFLAGS": "-I$$EXT_BUILD_DEPS/include",
        },
        "//conditions:default": {
            "CFLAGS": "-I$$EXT_BUILD_DEPS/openssl/include",
        },
    }),
    lib_source = ":all_srcs",
    out_lib_dir = "",
    out_shared_libs = [
        "liblicense_utils.so",
    ],
    postfix_script = select({
        "@platforms//os:macos": "cp liblicense_utils.so $$INSTALLDIR$$/liblicense_utils.dylib",
        "//conditions:default": "cp liblicense_utils.so $$INSTALLDIR$$/",
    }),
    targets = [
        "liblicense_utils.so -j" + KONG_VAR["NPROC"],
    ],
    visibility = ["//visibility:public"],
    deps = select({
        "@kong//:fips_flag": [
            "@boringssl_fips//:ssl",
            "@boringssl_fips//:crypto",
        ],
        "//conditions:default": ["@openssl//:openssl"],
    }),
)
